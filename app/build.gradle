apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-kapt'

android {
    def buildVersionCode = new Date()
            .format("yyMMddHHmm", TimeZone.getTimeZone("Europe/Istanbul"))
            .toInteger()

    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "developersancho.githubinginterview"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode buildVersionCode
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        buildConfigField("String", "ROOT_URL", "\"https://api.github.com/\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

}

android.applicationVariants.all { variant ->
    def appName = "githubingiterview"
    variant.outputs.all { output ->
        def newApkName = "${appName}-${variant.versionName}.apk"
        output.outputFileName = new File(newApkName)
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // material
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Bindings
    kapt "com.android.databinding:compiler:$android_plugin_version"

    // arch navigation component
    implementation 'androidx.navigation:navigation-fragment:2.0.0'
    implementation 'androidx.navigation:navigation-ui:2.0.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.0.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.0.0'

    // Koin
    implementation "org.koin:koin-core:2.0.1"
    implementation "org.koin:koin-android:2.0.1"
    implementation "org.koin:koin-androidx-scope:2.0.1"
    implementation "org.koin:koin-androidx-viewmodel:2.0.1"

    // Coroutines
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.3.0'
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-android', version: '1.3.0'

    // Networking
    implementation "com.squareup.retrofit2:retrofit:2.6.1"
    implementation "com.squareup.retrofit2:converter-gson:2.6.1"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation "com.squareup.okhttp3:okhttp:4.2.1"
    implementation "com.squareup.okhttp3:logging-interceptor:4.2.1"

    // ViewModel & LiveData
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.1.0"
    implementation "androidx.lifecycle:lifecycle-livedata:2.1.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.1.0"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'

    // for laoding
    implementation 'com.github.ybq:Android-SpinKit:1.4.0'
    implementation 'com.airbnb.android:lottie:3.0.7'

    // for log
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // for check memory leak
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.0-beta-3'
    // for see db table or pref xml file
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    // room
    implementation 'androidx.room:room-runtime:2.2.1'
    kapt "androidx.room:room-compiler:2.2.1"
    implementation 'androidx.room:room-ktx:2.2.1'

    // flashbar
    implementation 'com.andrognito.flashbar:flashbar:1.0.3'

    // test part
    testImplementation "org.koin:koin-test:2.0.1"
    testImplementation("com.squareup.okhttp3:mockwebserver:4.2.1")
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2"
    testImplementation "io.mockk:mockk:1.9.3"

    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'org.koin:koin-test:2.0.1'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2"

}
